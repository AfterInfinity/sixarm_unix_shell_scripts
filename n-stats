#!/bin/sh
set -euf -o pipefail

##
# Input a lit of numbers, output statistics
#
# Author: Joel Parker Henderson (joel@joelparkerhenderson.com)
# License: GPL
# Updated: 2015-01-25
##

sort -n |
awk '{
  sum += $1
  a[NR] = $1
}
END{
  n = NR
  min = a[1]
  max = a[NR]
  mean = sum/n
  range = max - min

  for (x = 1; x <= NR; x++){
    sumsq += (a[x] - mean) ** 2;
  }
  sd = sqrt(sumsq/NR)

  if (NR % 2) {
    median = a[(NR + 1) / 2];
  } else {
    median = (a[(NR / 2)] + a[(NR / 2) + 1]) / 2.0;
  }

  print "n:", n, "min:", min, "max:", max, "range:", range, "sum:", sum, "median:", median, "mean:", mean, "sd:", sd
}'
